import java.util.jar.Manifest

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

def includeProject(Project p, String taskName) {
    evaluationDependsOn(p.path)
    def targetTask = p.tasks.getByName(taskName)
    shadowJar.from(zipTree(targetTask.archiveFile)) {
        exclude "META-INF/MANIFEST.MF"
    }
    shadowJar.doFirst {
        def manifestFile = p.zipTree(targetTask.archiveFile).find { it.path.endsWith('MANIFEST.MF') }
        if (manifestFile != null) {
            manifestFile.withInputStream { is ->
                Manifest subManifest = new Manifest(is)
                def entriesToMerge = [:]
                subManifest.mainAttributes.each { key, value ->
                    if (key.toString() != 'Manifest-Version') {
                        entriesToMerge[key.toString()] = value.toString()
                    }
                }
                shadowJar.manifest {
                    attributes(entriesToMerge)
                }
            }
        }
    }
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

dependencies {
    //implementation(project(":imagemod-core"))
}

shadowJar {
    archiveBaseName.set(mod_id)
    archiveVersion.set(mod_version)
    archiveClassifier.set("")
    // include core functions
    //from project(":imagemod-core").sourceSets.main.output
    includeProject(project(":imagemod-neoforge"), "jar")
    includeProject(project(":imagemod-fabric"), "remapJar")
}