plugins {
    id 'java-library'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.139'
}

version = mod_version
group = "${project.mod_group}.neoforge"

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

neoForge {
    version = project.neo_version
    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }
    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation(project(":imagemod-core"))
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version       : neo_minecraft_version,
            minecraft_version_range : neo_minecraft_version_range,
            neo_loader_version_range: neo_loader_version_range,
            neo_version             : neo_version,
            neo_version_range       : neo_version_range,
            mod_license             : mod_license,
            mod_issue_url           : mod_issue_url,
            mod_id                  : mod_id,
            mod_version             : mod_version,
            mod_name                : mod_name,
            mod_authors             : mod_authors,
            mod_description         : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata
